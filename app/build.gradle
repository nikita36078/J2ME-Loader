apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "ru.playsoftware.j2meloader"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 43
        versionName "1.2.9"
        setProperty("archivesBaseName", "J2ME_Loader-$versionName")
        ndk {
            abiFilters 'x86', 'armeabi-v7a'
        }
    }
    flavorDimensions "default"

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }

    productFlavors {
        play {
            buildConfigField "boolean", "DONATIONS_GOOGLE", "true"
            versionNameSuffix "-play"
        }
        open {
            buildConfigField "boolean", "DONATIONS_GOOGLE", "false"
            versionNameSuffix "-open"
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/cpp/Android.mk'
        }
    }
}

dependencies {
    def acraVersion = '5.1.3'
    def roomVersion = '1.1.0'
    def supportLibVersion = '27.1.1'
    implementation "android.arch.persistence.room:runtime:$roomVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$roomVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:preference-v7:$supportLibVersion"
    implementation 'com.android.volley:volley:1.1.0'
    implementation "ch.acra:acra-dialog:$acraVersion"
    implementation 'com.github.billthefarmer:mididriver:v1.14'
    implementation 'com.github.yukuku:ambilwarna:2.0.1'
    implementation 'com.nononsenseapps:filepicker:4.2.1'
    implementation 'org.ow2.asm:asm-tree:5.2'
    implementation 'org.sufficientlysecure:donations:2.6'
    implementation project(':dexlib')
}
