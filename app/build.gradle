plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "ru.playsoftware.j2meloader"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 94
        versionName "1.7.5"
        setProperty("archivesBaseName", "J2ME_Loader-$versionName")
        ndk {
            abiFilters 'x86', 'armeabi-v7a'
        }
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    flavorDimensions "default"

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            jniDebuggable true
            multiDexEnabled true
            multiDexKeepProguard file('multidex-config.pro')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    productFlavors {
        play {
            buildConfigField "boolean", "DONATIONS_GOOGLE", "true"
            versionNameSuffix "-play"
            ndk {
                abiFilters 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
            }
        }
        open {
            buildConfigField "boolean", "DONATIONS_GOOGLE", "false"
            versionNameSuffix "-open"
        }
        fdroid {
            buildConfigField "boolean", "DONATIONS_GOOGLE", "false"
        }
        dev {
            buildConfigField "boolean", "DONATIONS_GOOGLE", "false"
            versionNameSuffix "-dev-" + generateVersionCode()
        }
    }

    bundle {
        language {
            enableSplit = false
        }
        density {
            enableSplit = false
        }
        abi {
            enableSplit = true
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/cpp/Android.mk'
        }
    }
    ndkVersion '22.1.7171670'
    lint {
        disable 'MissingTranslation'
    }
}

static def generateVersionCode() {
    def result = "git rev-list HEAD --count".execute().text.trim()
    return result.toInteger()
}

dependencies {
    def roomVersion = '2.4.1'
    def autoServiceVersion = "1.0"

    implementation project(':dexlib')

    // Android Jetpack
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.multidex:multidex:2.0.1'

    // Networking && APIs
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'com.google.code.gson:gson:2.8.8'
    //noinspection GradleDependency
    implementation 'com.github.nikita36078:donations:2.8'
    playImplementation 'com.github.nikita36078:donations:2.9'

    // Threads, Streams and Archives
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'net.lingala.zip4j:zip4j:2.8.0'

    // Audio/Video/Graphics
    implementation 'com.github.billthefarmer:mididriver:v1.21'
    implementation 'com.github.nikita36078:pngj:2.2.3'
    implementation 'com.github.nikita36078:mobile-ffmpeg:v4.3.2-compact'

    // Android UI
    implementation 'com.github.nikita36078:filepicker:4.3.0'
    implementation 'com.github.yukuku:ambilwarna:2.0.1'

    // Testing && CrashReports
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation 'junit:junit:4.13.2'
    implementation 'ch.acra:acra-dialog:5.8.3'

    // Other Libraries
    implementation 'org.ow2.asm:asm:9.1'
    compileOnly "com.google.auto.service:auto-service-annotations:$autoServiceVersion"
    annotationProcessor "com.google.auto.service:auto-service:$autoServiceVersion"
}
